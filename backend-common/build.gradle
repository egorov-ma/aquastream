plugins {
    id 'java-library'
    id 'maven-publish'
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-validation'
    
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    
    // Bucket4j for rate limiting
    api "com.bucket4j:bucket4j-redis:${bucket4jVersion}"

    implementation 'org.slf4j:slf4j-api'
    implementation 'ch.qos.logback:logback-classic'
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}"
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    testImplementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    // JUnit Jupiter already provided via root 'subprojects' dependency management
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'backend-common'
                description = 'AquaStream shared backend library (web/validation/security/ratelimit utilities)'
                url = 'https://github.com/egorov-ma/aquastream'
                licenses {
                    license {
                        name = 'Proprietary'
                        url = 'https://github.com/egorov-ma/aquastream'
                    }
                }
                developers {
                    developer {
                        id = 'aquastream'
                        name = 'AquaStream Team'
                    }
                }
                scm {
                    url = 'https://github.com/egorov-ma/aquastream'
                    connection = 'scm:git:https://github.com/egorov-ma/aquastream.git'
                    developerConnection = 'scm:git:ssh://git@github.com/egorov-ma/aquastream.git'
                }
            }
        }
    }
}
